---
layout: post
title: "Trustpilot challenge: dictionary-based multi-word anagram solver with a twist"
date: 2014-07-21 12:00 UTC
tags: [F#]
---
<div id="post">
<p>
Following the previous Trustpilot challenge
of <a href="http://bugfree.dk/blog/2014/07/17/trustpilot-challenge-crack-aes-encrypted-message">cracking
an AES encrypted message</a>, a new job ad challenge is up:
</p>

<a href="http://followthewhiterabbit.trustpilot.com/cs/step3.html">
  <img width="485" height="292" src="/content/TrustpilotAnagramChallenge.png" />
</a>

<p>
This challenge proved much harder than the previous. With an 18
character anagram, a brute-force approach would require trying out 18!
or 6,402,373,705,728,000 permutations of the letters (not counting
anagrams with spaces). This is clearly an intractable approach, which
is why Trustpilot provides a dictionary of 99,174 words to construct
anagrams from.
</p>

<p>To construct anagrams from a dictionary, I went with
a <a href="http://courses.cs.washington.edu/courses/cse143/08sp/handouts/23.html">recursive
backtracking algorithm</a>. It builds up a decision tree of dictionary
words, recursively following branches for as long as valid anagram can
be constructed from these (effectively trying out every valid
permutation of words from the dictionary), otherwise it backtracks.
</p>

<p>
Given the dictionary of 99,174 words, the problem is still
intractable. We can't simplify it further by including knowledge about
md5 because its output is uniformly distributed. Our only choice is to
further reduce the size of the dictionary before starting the
construction of the decision tree. This can be done through histogram
analysis as carried out by the F# functions below (see inline
comments):
</p>

<pre class="prettyprint lang-ml">
namespace TrustpilotChallenge

[&lt;AutoOpen&gt;]
module Anagram =
    // when two words are anagrams of each other, their alphabetized forms 
    // are equal, i.e., their letter histograms are equal
    let letterHistogram anagram =
        // get rid of spaces as they get inserted by the anagram solver
        // between each word from the dictionary, i.e., the number of
        // spaces in different anagrams is unimportant.
        anagram
        |&gt; Seq.toList
        |&gt; Seq.filter (fun c -&gt; c &lt;&gt; ' ')
        |&gt; Seq.groupBy (fun c -&gt; c) 
        |&gt; Map.ofSeq
        |&gt; Map.map (fun k v -&gt; Seq.length v)

    // subtracts letter histograms of two words if every frequency 
    // remains positive after the subtraction. The result is a 
    // new histogram of positive frequencies
    let inline subtractHistograms (h1: Map&lt;char, int&gt;) (h2: Map&lt;char, int&gt;) =
        let sharedAlphabet = h2 |&gt; Map.forall (fun k _ -&gt; h1.ContainsKey k)
        if sharedAlphabet then
            let diff =
                h1 |&gt; Map.map (fun w h -&gt; 
                    match h2.TryFind w with
                    | Some value -&gt; h - value
                    | None -&gt; h)
            let positives = diff |&gt; Map.forall (fun w h -&gt; h &gt;= 0)
            if positives then Some (diff |&gt; Map.filter (fun w h -&gt; h &gt; 0))
            else None
        else None

    let pruneWords anagram wordLetterHistograms =
        // only include dictionary words which has &lt;= same count 
        // of individual letters as the anagram
        let h1 = letterHistogram anagram
        wordLetterHistograms
        |&gt; Seq.filter (fun (_, h2) -&gt; 
            match subtractHistograms h1 h2 with
            | Some _ -&gt; true
            | None -&gt; false)

    let createWordLetterHistograms dictionary =
        dictionary |&gt; Seq.map (fun w -&gt; (w, letterHistogram w)) 

    let findAnagrams wordLetterHistograms anagram =
        let rec findAnagram' histogram wordsSoFar =
            seq {
                for (w, h2) in wordLetterHistograms do
                    match (subtractHistograms histogram h2) with
                    | Some h2 -&gt;
                        if h2 |&gt; Map.isEmpty 
                        then yield List.rev (w :: wordsSoFar)
                        else yield! findAnagram' h2 (w :: wordsSoFar)
                    | None -&gt; () }

        findAnagram' (letterHistogram anagram) List.empty&lt;string&gt;
</pre>

<p>
Removing duplicate words and pruning the dictionary using histogram
analysis, we end up with a dictionary of 1,658 words. The reduced-size
dictionary contains too many single and double letter words compared
to what's allowed in the English language which "artificially"
inflates the search space. In fact, assuming it's allowed to construct
anagrams by reusing single letter words, the search space grows toward
18! permutations.
</p>

<p>Single and double letter words or not, recursively constructing
permutations of around 1,600 words and backtracking when the
permutation is valid or invalid is still an intractable problem.
</p>

<p>
To make the problem tractable, we need to combine recursive
backtracking with heuristics, such as looking for solutions
constructed of longer words. Though it doesn't necessarily yield a
solution that also satisfies the md5 property, it does reduce the
search space significantly and makes exhaustive search possible:
</p>

<pre class="prettyprint lang-ml">
[&lt;AutoOpen&gt;]
module Hashing =
    open System
    open System.Text
    open System.Security.Cryptography

    let inline md5FromString s =
        // the compiler prforms overload resolution of GetBytes
        // based on parameter name. Otherwise we'd have to add
        // a type annotation
        BitConverter
            .ToString(MD5
                .Create()
                .ComputeHash(Encoding.UTF8.GetBytes(s = s)))
            .Replace("-", "")

module Program =  
    open System
    open System.IO

    [&lt;EntryPoint&gt;]
    let main _ =  
        let path = "../../wordlist"
        let anagram = "poultry outwits ants"
        let md5outcome = "4624d200580677270a54ccff86b9610e".ToUpper()
        let dictionary = 
            File.ReadAllLines(path)
            |&gt; Seq.distinct
            |&gt; Seq.filter (fun w -&gt; w.Length &gt; 3)
            |&gt; Seq.sortBy (fun w -&gt; w.Length)
            |&gt; createWordLetterHistograms
            |&gt; pruneWords anagram
            |&gt; Seq.toArray
            |&gt; Array.rev

        // iterative approach
        for anagram in (findAnagrams dictionary anagram) do
            let anagram' = anagram |&gt; List.reduce (fun acc w -&gt; acc + " " + w)
            if md5FromString anagram' = md5outcome then
                printfn "Secret: %s" anagram'
                Environment.Exit(0)       

        (* FP pipelining approach. Harder to debug than iterative one
        findAnagrams dictionary anagram
        |&gt; Seq.map (fun a -&gt; a |&gt; List.reduce (fun acc w -&gt; acc + " " + w))
        |&gt; Seq.map (fun a -&gt; (a, md5FromString a))
        |&gt; Seq.filter (fun (_, md5) -&gt; md5 = md5outcome)
        |&gt; Seq.map (fun (w, _) -&gt; w)
        |&gt; Seq.truncate 1
        |&gt; fun s -&gt; printfn "Secret: %A" s *)    
        0
</pre>

<p>
Perhaps adding heuristics to the equation makes for a less attractive
solution. It certainly requires a lot of trial and error to come up
with the right heuristics and not all heuristics are applicable to all
anagrams. In the end, recursive backtracking and heuristics yield a
solution in less than 30 seconds of running time.
</p>

</div>
