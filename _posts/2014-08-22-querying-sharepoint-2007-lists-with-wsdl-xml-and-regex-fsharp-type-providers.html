---
layout: post
title: "Querying SharePoint 2007 lists with WSDL, XML, and Regex F# type providers"
date: 2014-08-22 12:00 UTC
tags: [F#]
---
<div id="post">
<p>
In this post I'll describe how I created a WCF service to expose
product information from a legacy SharePoint 2007 system using the
WSDL, XML, and Regex F# type providers. Using F# together with the
various type providers means that very little actual code needs to be
written and most of it can be verified at compile time. Interfacing
with SharePoint has historically been a dynamic experience -- because
it's heavily XML based -- resulting in runtime errors.
</p>

<p>
The products to expose come in the form of list items within a
SharePoint Pages document library (but no knowledge of SharePoint is
required to follow along). In SharePoint, lists and list items are
like tables and rows in a relational database, which may be queried
through a SOAP web service and a propietary XML query language.
</p>

<p>Before being able to query, we must first authenticate with
SharePoint. The sections below explains the code required for
authenticating and querying in a top-down manner.
</p>

<h4>Helpers</h4>

<p>
The query that I sent to SharePoint and the results I get back are all
XML. However, the SharePoint APIs require the use of the old
XmlElement type where I prefer the new XElement type. Hence I need a
few routines to convert back and forth between the two. Also, the
username and password used for authentication is read from the config
file using a convenience function. I considered using the config type
provider, but it kept throwing a NullReferenceException.
</p>

<pre class="prettyprint lang-ml">
// requires the following NuGet packages:
//   FSharp.Data
//   RegexProvider

[&lt;AutoOpen&gt;]
module Utils =
    open System.Configuration
    open System.Xml.Linq
    open System.Xml

    let toXmlElement (e: XElement) =
        let d = XmlDocument()
        d.LoadXml(e.ToString())
        d.DocumentElement

    let toXElement (e: XmlElement) = XElement.Parse(e.OuterXml)
    let xn s = XName.Get s
    let getAppSetting (key: string) = ConfigurationManager.AppSettings.[key]
</pre>

<h4>Defining the domain model</h4>

<p>
For this post, I'll keep the domain model simple. It consists of a
Product type with basic metadata and optional references to documents
in the form of web resources:
</p>

<pre class="prettyprint lang-ml">
module Domain =
    open System

    type Document = { Title: string; Url: Uri }
    type Product = 
        { Title: string
          Url: Uri  
          Manual: Document option
          Datasheet: Document option
          Summary: string option
          Image: Uri option
          Description: string }
</pre>

<h4>Authenticating</h4>

<p>
With SharePoint 2007, before issuing requests against the API, you
must call the Authentication SOAP service passing in username and
password of a SharePoint user. The service then returns a
authentication cookie to pass along with subsequent calls. Here the
WSDL type provider comes in handy. Otherwise in an F# project I'd have
to create a seperate C# project, add a web reference to the
Authentication service's WSDL file, and reference that project from
the F# project.
</p>

<pre class="prettyprint lang-ml">
module ProductRepository =
    open System
    open System.Text.RegularExpressions
    open System.ServiceModel
    open System.ServiceModel.Channels
    open System.Xml.Linq
    open FSharp.Data
    open Microsoft.FSharp.Data.TypeProviders
    open FSharp.RegexProvider
    open Domain

    type AuthProxy = WsdlService&lt;"http://www.acme.com/_vti_bin/Authentication.asmx"&gt;
    type LoginErrorCode = AuthProxy.ServiceTypes.schemas.microsoft.com.sharepoint.soap.LoginErrorCode

    let getAuthCookie username password =
        new OperationContextScope(AuthProxy.GetAuthenticationSoap().DataContext.InnerChannel) |&gt; ignore
        let loginResult = AuthProxy.GetAuthenticationSoap().Login(username, password)
        
        if loginResult.ErrorCode &lt;&gt; LoginErrorCode.NoError then
            failwith "Unable to authenticate"

        let response = 
            OperationContext
                .Current
                .IncomingMessageProperties
                .[HttpResponseMessageProperty.Name] :?&gt; HttpResponseMessageProperty
        response.Headers.["Set-Cookie"]
</pre>

<p>
Calling getAuthCookie with a valid username and passwords results in
the following cookie being returned. The aspxauth part is unique for
every authentication request:
</p>

<pre>
".ASPXAUTH=2FD298088C07B99B5710F2F85A2599682321A0238156B36E4A5A70A986FD510A0D191
5756B55AFC95B53FBA6B6D3B3B68E5655DA63D8FCBF0775607C13E1E7E2D76B5F80DED397604EFB9
55B40FF83721C679146C7F4AF9E7F97B62C504170B27F666B58; path=/; HttpOnly"
</pre>

<h4>Querying</h4>

<p>
To query SharePoint 2007, you call the Lists SOAP service, but before
doing so you need to prepare the query. Preparing the query is
somewhat involved because you need to construct a block of XML telling
SharePoint with predicate I want to filter on and which fields of the
result set I want to be returned.
</p>

<p>Still in ProductRepository module, starting with a general function
to execute a query, again I call upon the WSDL type provider to
generate a proxy based on the Lists service's WSDL file. Notice how I
add the authCookie to the header of the request to pass it along when
I call GetListItems on the web service. The actual query is
constructed at the call site in the next section:
</p>

<pre class="prettyprint lang-ml">
    type ListProxy = WsdlService&lt;"http://www.acme.com/Products/_vti_bin/Lists.asmx"&gt;
    let execute query viewFields queryOptions =
        let username = getAppSetting "SharePointUsername"
        let password = getAppSetting "SharePointPassword"  
        let authCookie = getAuthCookie username password
        let client = ListProxy.GetListsSoap()
        let ctx = client.DataContext
               
        let binding = BasicHttpBinding(MaxReceivedMessageSize = 1024L * 1024L * 10L)
        ctx.Endpoint.Binding &lt;- binding
        ctx.Endpoint.Address &lt;- EndpointAddress("http://www.acme.com/Products/_vti_bin/Lists.asmx")

        new OperationContextScope(ctx.InnerChannel) |&gt; ignore
        let request = HttpRequestMessageProperty()
        request.Headers.["Cookie"] &lt;- authCookie
        OperationContext
            .Current
            .OutgoingMessageProperties
            .[HttpRequestMessageProperty.Name] &lt;- request :&gt; obj

        // see getProducts for query construction
        client.GetListItems(
            listName = "Pages", 
            viewName = null, 
            query = (query |&gt; toXmlElement), 
            viewFields = (viewFields |&gt; toXmlElement), 
            rowLimit = Int32.MaxValue.ToString(), 
            queryOptions = (queryOptions |&gt; toXmlElement), 
            webID = null) |&gt; toXElement
</pre>

<h4>Parsing result</h4>

<p>
Calling GetListItems on the Lists service executes the query against
the Pages lists and returns an XML result whose shape isn't all too
important for this example. I manually ran the query and stored a
subset of the XML result representing its shape in
ProductsTraining.xml. Feeding this XML file to the XML type provider
gives you strongly-typed access to the query result. For instance,
each product returned has a OwsEncodedAbsUrl element inferred to be of
type string, which I can access with full IntelliSense and
compile-time checking through p.OwsEncodedAbsUrl:
</p>

<p>
Now that I got back all the products, the final part is extracting the
pieces of information required to construct the Product type. For
legacy reasons -- and because editing rich-text fields in the browser
is easy for users -- this information is embedded within the (not
well-formed) HTML generated by SharePoint editor (by convention, both
documents are stored within the OwsPageX0020ContentX00203 element of
the result and the manual come before the datasheet).
</p>

<p> Thus, I use regular expressions with named groups to extract the
relevant pieces of information. The Regex type provider is a simple
provider that gives strongly typed access to named groups.
</p>

<pre class="prettyprint lang-ml">
    type Products = XmlProvider&lt;"ProductsTraining.xml"&gt;

    type DocumentsRegex = Regex&lt; @"&lt;a(.*?)href=""(?&lt;href&gt;(.*?))""(.*?)&gt;(?&lt;title&gt;(.*?))&lt;/a&gt;"&gt;
    let parseDocuments (p: Products.Row) =
        let documents = p.OwsPageX0020ContentX00203
        match documents with
        | None -&gt; (None, None)
        | Some ds -&gt; 
            let matches = DocumentsRegex().Matches(ds)
            let result =
                matches
                |&gt; Seq.map(fun m -&gt;
                    { Document.Title = m.title.Value
                      Url = Uri("http://www.acme.com" + m.href.Value) })
            (result |&gt; Seq.tryFind (fun m -&gt; m.Url.ToString().Contains("/Documents/Manuals/")),
                result |&gt; Seq.tryFind (fun m -&gt; m.Url.ToString().Contains("/Documents/datasheets/")))

    type ImageRegex = Regex&lt; @"&lt;img(.*?)src=""(?&lt;src&gt;(.*?))"""&gt;
    let parseImage (p: Products.Row) =
        let image = p.OwsPublishingPageImage
        match image with
        | None -&gt; None
        | Some i -&gt;            
            let m = ImageRegex().Match(i)
            match m.Success with
            | true -&gt; Some (Uri("http://www.acme.com" + m.src.Value))
            | false -&gt; None

    let parseProduct (p: Products.Row) =
        let manual, datasheet = parseDocuments p
        { Title = p.OwsTitle
          Url = Uri(p.OwsEncodedAbsUrl)
          Manual = manual
          Datasheet = datasheet
          Summary = p.OwsPublishingPageContent
          Image = parseImage p
          Description = p.OwsPageX0020ContentX00202 }

    let getProducts() =
        let query = 
            XElement(xn "Query",
                XElement(xn "Where",
                    XElement(xn "Contains",
                        XElement(xn "FieldRef", XAttribute(xn "Name", "PublishingPageLayout")),
                        XElement(xn "Value", XAttribute(xn "Type", "URL"), "Acme_Product.aspx"))))
        let fields = 
            ["Title"; "PublishingPageLayout"; "PublishingPageImage"; "PublishingPageContent"; 
              "Page_x0020_Content_x0020_2"; "Page_x0020_Content_x0020_3"; "EncodedAbsUrl"] 
            |&gt; List.fold (fun acc fld -&gt; 
                acc + XElement(xn "FieldRef", XAttribute(xn "Name", xn fld)).ToString()) ""
        
        let viewFields = XElement.Parse("&lt;ViewFields&gt;" + fields + "&lt;/ViewFields&gt;")
        let queryOptions = XElement(xn "QueryOptions", "")
        let result = execute query viewFields queryOptions
        let products = Products.Parse(result.ToString())       
        products.Data.Rows |&gt; Seq.map parseProduct |&gt; Seq.toList
</pre>

<p>
For completeness sake, the query to SharePoint looks as follows:
</p>

<pre class="prettyprint lang-xml">
&lt;Query&gt;
  &lt;Where&gt;
    &lt;Contains&gt;
      &lt;FieldRef Name="PublishingPageLayout" /&gt;
      &lt;Value Type="URL"&gt;Acme_Product.aspx&lt;/Value&gt;
    &lt;/Contains&gt;
  &lt;/Where&gt;
&lt;/Query&gt;

&lt;FieldRef Name="Title" /&gt;
&lt;FieldRef Name="PublishingPageLayout" /&gt;
&lt;FieldRef Name="PublishingPageImage" /&gt;
&lt;FieldRef Name="PublishingPageContent" /&gt;
&lt;FieldRef Name="Page_x0020_Content_x0020_2" /&gt;
&lt;FieldRef Name="Page_x0020_Content_x0020_3" /&gt;
&lt;FieldRef Name="EncodedAbsUrl" /&gt;

&lt;QueryOptions&gt;&lt;/QueryOptions&gt;
</pre>

<p>
Finally, here's how to execute the above code from a console. By the
same token, in the WCF project I create an instance of the
ProductRepository type. I decided to use the C# WCF template for the
front-end because it wasn't worth the hazzle of getting a full F# WCF
project working:
</p>

<pre class="prettyprint lang-ml">
[&lt;EntryPoint&gt;]
let main _ = 
    ProductRepository.getProducts()
    |&gt; Seq.truncate 1
    |&gt; printfn "%A"
    0
</pre>

</div>
