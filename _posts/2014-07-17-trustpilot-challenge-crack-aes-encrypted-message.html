---
layout: post
title: "Trustpilot challenge: crack AES encrypted message"
date: 2014-07-17 12:00 UTC
tags: [F#]
---
<div id="post">
<p>
Yesterday I came across a coding challenge posted by Trustpilot, a
Danish company looking to
hire. <a href="http://karriere.jobfinder.dk/artikel/kan-du-knaekke-dette-kodede-jobopslag-1912">According</a>
to the company, regular job ads have a too high signal to noise ratio
on skilled developers. So in order to submit our resume, first you
must decrypt the message to learn how.
</p>
<a href="http://followthewhiterabbit.trustpilot.com/challenge2.html">
  <img width="563" height="470" src="/content/TrustpilotAesChallenge.png" />
</a>

<p>
From the problem description, we see that a brute-force approach is
viable. Assuming the first six bytes of the key to be in the range
zero through 16, both inclusive, it yields a possible search space of
24,137,569 (17^6) keys.
</p>

<p>
First we need to get basic AES encryption and decryption
working. Strictly speaking, the encryption function isn't required,
but it helps validate the structure of the remaining solution in that
we can try out the approach on a smaller scale first.
</p>

<pre class="prettyprint lang-ml">
namespace TrustpilotChallenge

open System
open System.IO
open System.Security.Cryptography

[&lt;AutoOpen&gt;]
module Cryptography =
    let encryptStringToBytes (plainText: string) key iv =
        use aes = new AesManaged(BlockSize = 128, KeySize = 256)
        let encryptor = aes.CreateEncryptor(key, iv)
        use ms = new MemoryStream()
        use cs = new CryptoStream(ms, encryptor, CryptoStreamMode.Write)
        use sw = new StreamWriter(cs)
        sw.Write(plainText)

        // by explicitly closing the stream, we force flushing its contents.
        // Otherwise, calling ToArray() will yield an empty array. Note
        // that the C# using idiom handles this implicitly by disposing
        // the StreamWriter instance when the using block ends, whereas F# 
        // use disposition happens only when we exit the function. We 
        // could've used the using function instead.
        sw.Close()
        ms.ToArray()

    let decryptStringFromBytes (cipherText: byte[]) key iv =
        use aes = new AesManaged(BlockSize = 128, KeySize = 256)
        let decryptor = aes.CreateDecryptor(key, iv)
        use ms = new MemoryStream(cipherText)
        use cs = new CryptoStream(ms, decryptor, CryptoStreamMode.Read)
        use sr = new StreamReader(cs)
        sr.ReadToEnd()
</pre>

<p>
Next comes the main part which makes use of the Cryptography functions
just defined. Rather than describe the code here, I've embedded
comments about the approaches I took directly in the source code. The
tricky part is validating when a key is indeed the correct one.
</p>

<pre class="prettyprint lang-ml">
module Program =  
    [&lt;EntryPoint&gt;]
    let main args =
        if args.Length &lt;&gt; 2 then 
            printfn "Must call TrustpilotChallenge with batch begin and end args"
            printfn "Example: TrustpilotChallenge.exe 0 3"
            Environment.Exit(1)

        let batchRange = [|Convert.ToByte(args.[0])..Convert.ToByte(args.[1])|]
        let encrypted = Convert.FromBase64String("yptyoDdVBdQtGhgoePppYHnWyugGmy0j81sf3zBeUXEO/LYRw+2XmVa0/v6YiSy9Kj8gMn/gNu2I7dPmfgSEHPUDJpNpiOWmmW1/jw/Pt29Are5tumWmnfkazcAb23xe7B4ruPZVxUEhfn/IrZPNZdr4cQNrHNgEv2ts8gVFuOBU+p792UPy8/mEIhW5ECppxGIb7Yrpg4w7IYNeFtX5d9W4W1t2e+6PcdcjkBK4a8y1cjEtuQ07RpPChOvLcSzlB/Bg7UKntzorRsn+y/d72qD2QxRzcXgbynCNalF7zaT6pEnwKB4i05fTQw6nB7SU1w2/EvCGlfiyR2Ia08mA0GikqegYA6xG/EAGs3ZJ0aQUGt0YZz0P7uBsQKdmCg7jzzEMHyGZDNGTj0F2dOFHLSOTT2/GGSht8eD/Ae7u/xnJj0bGgAKMtNttGFlNyvKpt2vDDT3Orfk6Jk/rD4CIz6O/Tnt0NkJLucHtIyvBYGtQR4+mhbfUELkczeDSxTXGDLaiU3de6tPaa0/vjzizoUbNFdfkIly/HWINdHoO83E=")
        let iv = Convert.FromBase64String("DkBbcmQo1QH+ed1wTyBynA==");

        // generate keys lazily
        let genKeys batchRange =
            seq {
                for b0 in batchRange do
                    printfn "%s: Starting batch: %A" (DateTime.Now.ToLongTimeString()) b0
                    for b1 in [0uy..16uy] do
                        for b2 in [0uy..16uy] do
                            for b3 in [0uy..16uy] do
                                for b4 in [0uy..16uy] do
                                    for b5 in [0uy..16uy] do
                                        yield [|b0;b1;b2;b3;b4;b5;0uy;0uy;
                                                0uy;0uy;0uy;0uy;0uy;0uy;0uy;0uy;
                                                0uy;0uy;0uy;0uy;0uy;0uy;0uy;0uy;
                                                0uy;0uy;0uy;0uy;0uy;0uy;0uy;0uy|] }

        // validate key when found (need to run for yourself)
        //let key = [|1uy;2uy;3uy;4uy;5uy;6uy;0uy;0uy;
        //            0uy;0uy;0uy;0uy;0uy;0uy;0uy;0uy;
        //            0uy;0uy;0uy;0uy;0uy;0uy;0uy;0uy;
        //            0uy;0uy;0uy;0uy;0uy;0uy;0uy;0uy|]
        //let decrypted = decryptStringFromBytes encrypted key iv
        //printfn "%s" decrypted
 
        for key in genKeys batchRange do
            // uncomment for testing purpose
            //let testMessage = "Hello world"
            //let encryptedMessage = encryptStringToBytes testMessage key iv
            //let decryptedMessage = decryptStringFromBytes encryptedMessage key iv            

            try                                                              
                let decrypted = decryptStringFromBytes encrypted key iv
                
                // an attempt assumming decrypted text contains known token
                if decrypted.ToLower().Contains("trust") 
                then printfn "Candidate key by 'trust': %A" key.[0..5]

                // an attempt using simple statistical analysis by counting the number
                // of characters whose high-order bit isn't set, which indicates a 
                // simple ASCII result. Results in too many false positives
                //let highOrderBitSet = 
                //    decrypted.ToCharArray() 
                //    |&gt; Array.filter (fun c -&gt; c |&gt; byte &&& 255uy = 255uy)
                //    |&gt; Array.length
                //if highOrderBitSet = 0 then printfn "Candidate key by no high-order bits set: %A" key.[0..5]

                // an attempt based on erronous assumption of AES symmetry such that: 
                //   decrypt(encrypt(plain text)) = plain text (correct)
                //   encrypt(decrypt(cipher text)) = cipher text (wrong)
                //let reEncrypted = encryptStringToBytes decrypted key iv
                //if reEncrypted = encrypted then printfn "Candidate key by re-encrypt: %A" key.[0..5]
            with
            | :? CryptographicException -&gt; ()
            
        Console.WriteLine("Hit any key to exit")
        Console.Out.Flush()
        Console.ReadKey() |&gt; ignore
        0
</pre>

<p>
The console application must be run from the command-line, passing in
the part of the search space to cover. This way, we can easily
parallelize the search by running multiple console application. Say we
have a computer with four cores, then we can issue the following four
commands to (almost) evenly distribute the search:
</p>

<pre>
% TrustpilotChallenge.exe 0 3
% TrustpilotChallenge 4 7
% TrustpilotChallenge 8 11
% TrustpilotChallenge 12 16
</pre>

<p>
On my laptop with four cores running at 2.4 GHz, it takes about 30
minutes to traverse the entire search space. Luckily, the current
approach yields one candidate key.
</p>

</div>
